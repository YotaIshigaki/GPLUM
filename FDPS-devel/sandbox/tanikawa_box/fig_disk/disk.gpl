set lmargin 7
set rmargin 2
set tmargin 2
set bmargin 3

set term po co eps enhanced "TimesNewRoman"
set output "tmp.eps"

set multiplot

set si ra 1
set si 0.8

dx=0.355
dy=0.51

set xrange [-25:25]
#set xtic -40, 20, 40
#set mxtic 5
set format x ""

set yrange [-25:25]
#set ytic -40, 20, 40
#set mytic 5
set format y ""

unset key

set pm3d
set pm3d map
set ticslevel 0
set cbrange[0:1]
set palette defined (0 "black", 0.5 "light-blue", 1 "white")
unset colorbox

#============================================================

set origin 0*dx, 1*dy

set style arrow 1 heads size screen 0.008,90 ls 2

set label 1 "t = 0.5 Gyr" at -24, -22 left textcolor rgb "#ffffff" font ",25" front
set label 2 "15kpc (4.9x10^4ly)" at -24, 22 left textcolor rgb "#ffffff" font ",25" front
set arrow 1 from -20, 18 to -5, 18 as 1 lc rgb "#ffffff" lw 3 lt 1 front

splot \
"out/snap_050.con" with pm3d

#============================================================

set origin 1*dx, 1*dy

set label 1 "t = 1 Gyr"
unset label 2
unset arrow 1

splot \
"out/snap_100.con" with pm3d

#============================================================

set origin 0*dx, 0*dy

set label 1 "t = 2 Gyr"

splot \
"out/snap_200.con" with pm3d

#============================================================

set origin 1*dx, 0*dy

set label 1 "t = 3 Gyr"

splot \
"out/snap_300.con" with pm3d
